// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'versions.gradle'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha06'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.6.1'
}

subprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url "https://androidx.dev/snapshots/builds/$versions.snapshot/artifacts/repository/" }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(versions.ktlint)//.userData([android: "true"])
            licenseHeaderFile rootProject.file('spotless/license.kt')
        }
        java {
            target '**/*.java'
            targetExclude("$buildDir/**/*.java")
            targetExclude('bin/**/*.java')

            licenseHeaderFile rootProject.file('spotless/license.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-Xallow-unstable-dependencies'
            jvmTarget = "1.8"
        }
    }
}